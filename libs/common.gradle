// include this file in subprojects:
// apply from: rootProject.file('libs/common.gradle')

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish' // need this for installing artifacts locally


repositories {
  mavenLocal()
  mavenCentral()
  // needed by org.apache.maven:maven-ant-tasks:2.1.4-SNAPSHOT :
  maven {
    url 'http://repository.apache.org/content/groups/snapshots'
    allowInsecureProtocol = true
  }
}

group = rootProject.group
version = rootProject.version

dependencies {
  testImplementation "org.spockframework:spock-core:${spock_version}", {
    exclude group: 'org.codehaus.groovy', module: 'groovy-all'
  }
  testImplementation "org.codehaus.groovy:groovy-all:${groovy_version}"
}

sourceCompatibility = '13'
targetCompatibility = '13'

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates sources jar') {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, description: 'Creates javadoc jar') {
  dependsOn javadoc
  classifier = 'javadoc'
  from javadoc.destinationDir
  if(tasks.findByName('groovydoc')) {
    dependsOn groovydoc
    from groovydoc.destinationDir
  }
}

artifacts {
  archives sourcesJar, javadocJar
}

